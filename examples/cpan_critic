#!/Users/brian/bin/perls/perl5.20.0
use v5.20;
use feature qw(postderef);
no warnings qw(experimental::postderef);

use FindBin qw($Bin);
use File::Basename;
use Term::ANSIColor qw(:constants);

use lib "$Bin/lib";
use lib qw(
	/Users/brian/Dev/ReturnValue/lib
	);

use CPAN::Critic;

my $critic = CPAN::Critic->new;

@ARGV = '.' unless @ARGV;

# find . -name "Makefile.PL" -print0 | xargs -0 cpan-critic
my @dirs = map {
	/Makefile.PL$/ ? dirname($_) : $_;
	} @ARGV;


my $PASS = GREEN . 'PASS' . RESET;
my $FAIL = RED   . 'FAIL' . RESET;

foreach my $dir ( @dirs ) {
	say "===========Processing $dir";
	my $rc = $critic->critique( $dir );

	if( $rc->is_success ) {
		# these are all the result objects from the policies
		my $results = $rc->value;

		foreach my $result ( $results->@* ) {
			printf "%4s %s\n", $result->is_success ? $PASS : $FAIL, $result->policy;

			if( $result->is_error ) {
				say "\tProblem: ";
				my $problems = $result->value;
				foreach my $problem ( $result->value->@* ) {
					say "\t\t" . $problem->description;
					}
				}
			}
		}
	else {
		say "Big problem: ", $rc->description;
		}
	}

=back

=head1 TO DO


=head1 SOURCE AVAILABILITY

This code is in Github:

	http://github.com/briandfoy/cpan-critic

=head1 AUTHOR

brian d foy, C<< <bdfoy@cpan.org> >>

=head1 COPYRIGHT AND LICENSE

Copyright Â© 2014-2015, brian d foy <bdfoy@cpan.org>. All rights reserved.

You may redistribute this under the same terms as Perl itself.

=cut

1;

